description = 'Axelor\'s WS builder'

buildscript {
    repositories {
        mavenCentral() {
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven { url 'https://repository.axelor.com/nexus/repository/maven-public/' }

        // Declare the Node.js download repository
        ivy {
            name = "Node.js"
            setUrl("https://nodejs.org/dist/")
            patternLayout {
                artifact("v[revision]/[artifact](-v[revision]-[classifier]).[ext]")
            }
            metadataSources {
                artifact()
            }
            content {
                includeModule("org.nodejs", "node")
            }
        }
    }
    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:5.0.0"
    }
}

apply plugin: 'base'
apply plugin: 'com.github.node-gradle.node'

node {
    version = '18.16.1'
    pnpmVersion = '8.6.6'
    download = true
}

tasks.register('wsBuilderBuild', PnpmTask) {
    group = 'axelor'
    description = 'Build WS builder'
    environment = [
            CI: 'false'
    ]

    outputs.dir('build')
    inputs.files('package.json', 'pnpm-lock.yaml')
    inputs.dir('src')
    inputs.dir('public')

    dependsOn 'wsBuilderInstall'
    mustRunAfter 'wsBuilderInstall'
    args = ["build"]
}

tasks.register('wsBuilderInstall', PnpmTask) {
    group = 'axelor'
    description = 'Install WS builder dependencies'
    environment = [
            CI: 'false'
    ]

    args = ["install"]
}

tasks.register("reactBuild", GradleBuild) {
    tasks = ["wsBuilderBuild"]
}

clean {
    delete "build", "node_modules"
}
