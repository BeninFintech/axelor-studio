description = 'Axelor\'s WS builder'

buildscript {
    repositories repos
    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:3.5.1"
    }
}

apply plugin: 'base'
apply plugin: 'com.github.node-gradle.node'

node {
    version = '16.17.0'
    yarnVersion = '1.22.19'
    download = true
}

tasks.register('wsBuilderBuild', YarnTask) {
    group = 'axelor'
    description = 'Build WS builder'
    environment = [
            CI: 'false'
    ]

    outputs.dir('build')
    inputs.files('package.json', 'package-lock.json')
    inputs.dir('src')
    inputs.dir('public')

    dependsOn 'wsBuilderInstall'
    args = ["build"]
}

tasks.register('wsBuilderInstall', YarnTask) {
    group = 'axelor'
    description = 'Install WS builder dependencies'
    environment = [
            CI: 'false'
    ]

    args = ["install"]
}

clean {
    delete "build", "node_modules"
}
