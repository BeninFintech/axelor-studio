description = 'Axelor\'s Baml editor'

buildscript {
    repositories {
        mavenCentral() {
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven { url 'https://repository.axelor.com/nexus/repository/maven-public/' }

        // Declare the Node.js download repository
        ivy {
            name = "Node.js"
            setUrl("https://nodejs.org/dist/")
            patternLayout {
                artifact("v[revision]/[artifact](-v[revision]-[classifier]).[ext]")
            }
            metadataSources {
                artifact()
            }
            content {
                includeModule("org.nodejs", "node")
            }
        }
    }
    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:5.0.0"
    }
}

apply plugin: 'base'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.node-gradle.node'

node {
    version = '18.16.1'
    pnpmVersion = '8.6.6'
    download = true
}

tasks.register('bamlBuild', PnpmTask) {
    group = 'axelor'
    description = 'Build Baml editor'
    environment = [
            CI: 'false'
    ]

    outputs.dir("$projectDir/build")
    inputs.files("$projectDir/package.json", "$projectDir/pnpm-lock.yaml")
    inputs.dir("$projectDir/src")
    inputs.dir("$projectDir/public")

    dependsOn 'bamlInstall'
    mustRunAfter 'bamlInstall'
    args = ["build"]
}

tasks.register('bamlInstall', PnpmTask) {
    group = 'axelor'
    description = 'Install Baml editor dependencies'
    environment = [
            CI: 'false'
    ]

    args = ["install"]
}


tasks.register("reactBuild", GradleBuild) {
    tasks = ["bamlBuild"]
}

tasks.register("reactClean", Delete) {
    delete "build", "node_modules"
}

clean {
    dependsOn "reactClean"
    mustRunAfter "reactClean"
}
