description = 'Axelor\'s BPM webapp'

buildscript {
    repositories {
        mavenCentral() {
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
            content { excludeGroupByRegex "com\\.axelor.*" }
        }
        maven { url 'https://repository.axelor.com/nexus/repository/maven-public/' }

        // Declare the Node.js download repository
        ivy {
            name = "Node.js"
            setUrl("https://nodejs.org/dist/")
            patternLayout {
                artifact("v[revision]/[artifact](-v[revision]-[classifier]).[ext]")
            }
            metadataSources {
                artifact()
            }
            content {
                includeModule("org.nodejs", "node")
            }
        }
    }
    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:5.0.0"
    }
}

apply plugin: 'base'
apply plugin: 'com.github.node-gradle.node'

node {
    version = '18.16.1'
    pnpmVersion = '8.6.6'
    download = true
}

tasks.register('bpmBuild', PnpmTask) {
    group = 'axelor'
    description = 'Build BPM webapp'
    environment = [
            CI: 'false'
    ]

    outputs.dir("$projectDir/apps/bpm/build")
    inputs.files("$projectDir/package.json", "$projectDir/pnpm-lock.yaml",
            "$projectDir/apps/bpm/package.json", "$projectDir/apps/bpm/pnpm-lock.yaml",
            "$projectDir/apps/generic-builder/package.json", "$projectDir/apps/generic-builder/pnpm-lock.yaml",
            "$projectDir/apps/mapper/package.json", "$projectDir/apps/mapper/pnpm-lock.yaml",
            "$projectDir/apps/timer-builder/package.json")
    inputs.dir("$projectDir/apps/bpm/src")
    inputs.dir("$projectDir/apps/bpm/public")
    inputs.dir("$projectDir/apps/generic-builder/src")
    inputs.dir("$projectDir/apps/generic-builder/public")
    inputs.dir("$projectDir/apps/mapper/src")
    inputs.dir("$projectDir/apps/mapper/public")
    inputs.dir("$projectDir/apps/timer-builder/src")
    inputs.dir("$projectDir/apps/timer-builder/public")

    dependsOn 'bpmInstall'
    mustRunAfter 'bpmInstall'
    args = ["build"]
}

tasks.register('bpmInstall', PnpmTask) {
    group = 'axelor'
    description = 'Install BPM webapp dependencies'

    args = ["install"]
}

tasks.register('mapperBuild', PnpmTask) {
    group = 'axelor'
    description = 'Build BPM webapp'
    environment = [
            CI: 'false'
    ]

    workingDir = file("apps/mapper")

    outputs.dir("$projectDir/apps/mapper/build")
    inputs.files("$projectDir/apps/mapper/package.json", "$projectDir/apps/mapper/pnpm-lock.yaml")
    inputs.dir("$projectDir/apps/mapper/src")
    inputs.dir("$projectDir/apps/mapper/public")

    dependsOn 'mapperInstall'
    mustRunAfter 'mapperInstall'
    args = ["build"]
}

tasks.register('mapperInstall', PnpmTask) {
    group = 'axelor'
    description = 'Install BPM webapp dependencies'
    environment = [
            CI: 'false'
    ]

    workingDir = file("apps/mapper")

    args = ["install"]
}


tasks.register("reactBuild", GradleBuild) {
    tasks = ["bpmBuild", "mapperBuild"]
}

clean {
    delete "build", "node_modules",
            "apps/bpm/build", "apps/bpm/node_modules",
            "apps/mapper/build", "apps/mapper/node_modules"
}
