description = 'Axelor\'s Studio'

buildscript {
	repositories {
		mavenCentral() {
			content { excludeGroupByRegex "com\\.axelor.*" }
		}
		maven {
			url 'https://plugins.gradle.org/m2/'
			content { excludeGroupByRegex "com\\.axelor.*" }
		}
		maven { url 'https://repository.axelor.com/nexus/repository/maven-public/' }

		// Declare the Node.js download repository
		ivy {
			name = "Node.js"
			setUrl("https://nodejs.org/dist/")
			patternLayout {
				artifact("v[revision]/[artifact](-v[revision]-[classifier]).[ext]")
			}
			metadataSources {
				artifact()
			}
			content {
				includeModule("org.nodejs", "node")
			}
		}
	}
	dependencies {
		classpath "com.github.node-gradle:gradle-node-plugin:3.5.1"
	}
}

apply plugin: 'base'
apply plugin: 'com.github.node-gradle.node'

node {
	version = '16.17.0'
	yarnVersion = '1.22.19'
	download = true
}

tasks.register('studioBuild', YarnTask) {
	group = 'axelor'
	description = 'Build Studio'
	environment = [
		CI: 'false'
	]

	outputs.dir('build')
	inputs.files('package.json', 'package-lock.json')
	inputs.dir('src')
	inputs.dir('public')

	dependsOn 'studioInstall'
	args = ["build"]
}

tasks.register('studioInstall', YarnTask) {
	group = 'axelor'
	description = 'Install Studio dependencies'
	environment = [
		CI: 'false'
	]

	args = ["install"]
}

clean {
	delete "build", "node_modules"
}
