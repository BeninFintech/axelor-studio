ext {
    bpmRootPath = "react-components/bpm-webapp"
    bpmBuildPath = "${bpmRootPath}/apps/bpm/build"
}

task yarnBpmInstall(type: YarnTask) {
    group = 'com.axelor.addons'
    description = 'Install dependencies'
    workingDir = file("${bpmRootPath}")

    args = ['install']
}

task yarnBpmBuild(type: YarnTask) {
    group = 'com.axelor.addons'
    description = 'Build App'
    environment = [
            CI: 'false'
    ]
    workingDir = file("${bpmRootPath}")

    dependsOn yarnBpmInstall
    mustRunAfter yarnBpmInstall

    outputs.upToDateWhen { false }
    outputs.dir('build')

    args = ["run", "build"]
}

task copyBpmWebapp(type: Copy) {
    group = 'com.axelor.addons'
    description = 'Copy into webapp'

    dependsOn yarnBpmBuild
    mustRunAfter yarnBpmBuild

    destinationDir = file(rootProject.buildDir)
    into("webapp/wkf-editor") {
        from "${bpmBuildPath}"
    }
}

rootProject.tasks.named("clean") {
    delete "${bpmBuildPath}"
    delete "${bpmRootPath}/apps/bpm/node_modules"
    delete "${bpmRootPath}/apps/generic-builder/node_modules"
    delete "${bpmRootPath}/apps/mapper/node_modules"
    delete "${bpmRootPath}/apps/timer-builder/node_modules"
    delete "${bpmRootPath}/node_modules"
}

rootProject.tasks.named("war") {
    dependsOn copyBpmWebapp
    mustRunAfter copyBpmWebapp
}

rootProject.tasks.named("compileJava") {
    dependsOn copyBpmWebapp
    mustRunAfter copyBpmWebapp
}

rootProject.tasks.named("processResources") {
    dependsOn copyBpmWebapp
    mustRunAfter copyBpmWebapp
}