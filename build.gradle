import com.axelor.gradle.tasks.GenerateChangelog

buildscript {
	ext.repos = {
		mavenCentral() {
			content { excludeGroupByRegex "com\\.axelor.*" }
		}
		maven {
			url 'https://plugins.gradle.org/m2/'
			content { excludeGroupByRegex "com\\.axelor.*" }
		}
		maven { url 'https://repository.axelor.com/nexus/repository/maven-public/' }

		// Declare the Node.js download repository
		ivy {
			name = "Node.js"
			setUrl("https://nodejs.org/dist/")
			patternLayout {
				artifact("v[revision]/[artifact](-v[revision]-[classifier]).[ext]")
			}
			metadataSources {
				artifact()
			}
			content {
				includeModule("org.nodejs", "node")
			}
		}
	}
	ext.aopVersion = '6.1.3'
	repositories repos
	dependencies {
		classpath "com.axelor:axelor-gradle:${aopVersion}"
		classpath "com.github.node-gradle:gradle-node-plugin:3.5.1"
	}
}

repositories repos

apply plugin: 'com.github.node-gradle.node'

apply from: "version.gradle"
apply plugin: "com.axelor.app"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = "com.axelor.addons"
description "Axelor Studio Module"

axelor {
	title "Axelor Studio"
	description "Axelor Studio Module"
}

test {
	exclude "com/axelor/apps/baml/test/**"
}

dependencies {
	if (file("../axelor-message").exists()) {
		api project(":modules:axelor-message")
	}
	else {
		api "com.axelor.addons:axelor-message:1.0.1"
	}

	api "org.camunda.bpm:camunda-engine:7.17.0"
	api "org.camunda.bpm:camunda-engine-plugin-spin:7.17.0"
	api "org.camunda.spin:camunda-spin-dataformat-json-jackson:1.17.0"

	api "org.apache.commons:commons-lang3:3.2"
	api "com.opencsv:opencsv:4.6"

	implementation "org.codehaus.groovy:groovy-all:3.0.9"
	implementation "org.eclipse.birt.runtime.3_7_1:Tidy:1"
	implementation "org.apache.commons:commons-exec:1.2"
	implementation "org.apache.commons:commons-text:1.9"
	implementation 'org.jboss.resteasy:resteasy-jaxb-provider:4.7.7.Final'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.4'
}

tasks.register('generateChangelog', GenerateChangelog) {
	description "Generate changelog from unreleased entries."
	group "Axelor"
	files = fileTree(projectDir) {
		include 'changelogs/unreleased/*.yml'
		include 'changelogs/unreleased/*.yaml'
	}
}

node {
	version = '16.17.0'
	npmVersion = '8.15.0'
	yarnVersion = '1.22.19'
	download = true
}

apply from: './gradle/wkf-editor.gradle'
apply from: './gradle/studio-custom-model.gradle'
apply from: './gradle/baml-editor.gradle'
apply from: './gradle/ws-builder.gradle'

processResources {
	dependsOn bamlBuild
	dependsOn studioBuild
	dependsOn bpmBuild
	dependsOn wsBuilderBuild

	from(file("$projectDir/react-components/baml/build")) {
		include "**"
		into "webapp/baml-editor"
	}

	from(file("$projectDir/react-components/studio/build")) {
		include "**"
		into "webapp/studio/custom-model"
	}

	from(file("$projectDir/react-components/bpm-webapp/apps/bpm/build")) {
		include "**"
		into "webapp/wkf-editor"
	}

	from(file("$projectDir/react-components/bpm-webapp/apps/mapper/build")) {
		include "**"
		into "webapp/mapper"
	}

	from(file("$projectDir/react-components/webservices-builder/build")) {
		include "**"
		into "webapp/ws-builder"
	}
}

//
// Publish
//

apply plugin: 'maven-publish'

ext {
	mavenUsername = project.findProperty("addonsMavenUsername")
	mavenPassword = project.findProperty("addonsMavenPassword")
}

publishing {
	repositories {
		maven {
			def repoPath = project.hasProperty("finalRelease")
					? "maven-releases"
					: "maven-snapshots"
			name = "maven"
			url = "https://repository.axelor.com/nexus/repository/" + repoPath
			credentials {
				username = project.mavenUsername
				password = project.mavenPassword
			}
		}
	}
}
